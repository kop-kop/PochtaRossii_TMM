
#Область СлужебныйПрограммныйИнтерфейс

&НаСервере
Процедура ИнициализироватьКарту(КодМаршрута = "")

	Если СтрНайти(ВРЕГ(КодМаршрута), "<!DOCTYPE HTML") Тогда
		ТекстHTML = КодМаршрута;
		Возврат;
	КонецЕсли;
	Текст = ПолучитьТекстМакета();
	Если ТаблицаАдресов.Количество() = 0 Тогда
		ШиротаЦентр = "44.1471449";
		ДолготаЦентр = "43.0122609";
	Иначе
		ШиротаЦентр = Формат(ТаблицаАдресов[0].Широта, "ЧЦ=15; ЧДЦ=12; ЧРД=.; ЧГ=0");
		ДолготаЦентр = Формат(ТаблицаАдресов[0].Долгота, "ЧЦ=15; ЧДЦ=12; ЧРД=.; ЧГ=0");
	КонецЕсли;
	Текст = СтрЗаменить(Текст, "&ШиротаЦентр", ШиротаЦентр);
	Текст = СтрЗаменить(Текст, "&ДолготаЦентр", ДолготаЦентр);
	ТекстHTML = СтрЗаменить(Текст, "&КодМаршрута", КодМаршрута);

КонецПроцедуры

&НаСервере
Функция ПолучитьТекстМакета()

	Результат = "<!DOCTYPE html>
	|<html>
	|<head>
	|<meta http-equiv=""Content-Type"" content=""text/html; charset=utf-8""/>
	|<meta http-equiv=""X-UA-Compatible"" content=""IE=9""/>
	|<link rel=""stylesheet"" href=""https://unpkg.com/leaflet@1.7.1/dist/leaflet.css"" />
	|<script src=""https://unpkg.com/leaflet@1.7.1/dist/leaflet.js""></script>
	|</head>
	|<body>
	|<div id=""map"" class=""map"" style=""position: absolute; top: 0px; right: 0px; bottom: 0px; left: 0px;""></div>
	|<script type=""text/javascript"">
	|var map = L.map('map');
	|map.setView([&ШиротаЦентр, &ДолготаЦентр], 14);
	|L.tileLayer('http://{s}.tile.osm.org/{z}/{x}/{y}.png', {
	|attribution: '&copy; <a href=""http://osm.org/copyright"">OpenStreetMap</a> contributors'
	|}).addTo(map);
	|&КодМаршрута
	|</script>
	|</body>
	|</html>";
	Возврат Результат;

КонецФункции

&НаСервере
Процедура ГеокодироватьOSR()

	Обработка = РеквизитФормыВЗначение("Объект");
	Координаты = Обработка.ГеокодироватьАдрес(ТекАдрес);
	Если Координаты <> Неопределено Тогда
		стрАдрес = ТаблицаАдресов.Добавить();
		стрАдрес.Широта = Координаты.Широта;
		стрАдрес.Долгота = Координаты.Долгота;
		стрАдрес.Адрес = Координаты.Адрес;
	КонецЕсли;
	МассивАдресов = Новый Массив();
	Для каждого стрАдрес Из ТаблицаАдресов Цикл
		МассивАдресов.Добавить(Новый Структура("Адрес,Широта,Долгота", стрАдрес.Адрес, стрАдрес.Широта, стрАдрес.Долгота));
	КонецЦикла;
	Если МассивАдресов.Количество() Тогда
		КодМаршрута = Обработка.СформироватьJavaScriptМаршрута(МассивАдресов);
	Иначе
		КодМаршрута = "";
	КонецЕсли;
	ИнициализироватьКарту(КодМаршрута);

КонецПроцедуры

&НаСервере
Процедура ПостроитьМаршрутOSR()

	КодМаршрута = РеквизитФормыВЗначение("Объект").ВыполнитьРасчетМаршрута(ТаблицаАдресов.Выгрузить());
	ИнициализироватьКарту(КодМаршрута);

КонецПроцедуры

&НаКлиенте
Процедура ПоискАдреса(Адрес)

	ГеокодироватьOSR();

КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура НайтиАдрес(Команда)

	ПоискАдреса(ТекАдрес);

КонецПроцедуры


#Область ОбработчикиСОбытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ИнициализироватьКарту();

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПостроитьМаршрут(Команда)

	Если ТаблицаАдресов.Количество() <= 1 Тогда
		ПоказатьПредупреждение(, "Недостаточно точек для построение маршрута!");
		Возврат;
	КонецЕсли;
	
	ПостроитьМаршрутOSR();

КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)

	ТаблицаАдресов.Очистить();
	ИнициализироватьКарту();

КонецПроцедуры

&НаКлиенте
Процедура ТекАдресПриИзменении(Элемент)

	ПоискАдреса(ТекАдрес);

КонецПроцедуры

#КонецОбласти


