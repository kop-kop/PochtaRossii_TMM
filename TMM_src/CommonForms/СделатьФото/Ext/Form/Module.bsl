
&НаКлиенте
Процедура Сфотографировать(Команда)
	
	РасширениеФайла = "";
	Если НЕ ПустаяСтрока(Фото) Тогда
		Попытка
			УдалитьИзВременногоХранилища(Фото);
		Исключение
		КонецПопытки;
		Фото = "";
	КонецЕсли;
	Результат = Неопределено;
	
	#Если МобильноеПриложениеКлиент Тогда	
	
	Если СредстваМультимедиа.ПоддерживаетсяФотоснимок(Камера) Тогда
		
		ТипПодсветки = ?(Подсветка,ТипПодсветкиКамеры.Включена,ТипПодсветкиКамеры.Выключена);
		
		Результат = СредстваМультимедиа.СделатьФотоснимок(Камера,,,,ТипПодсветки);
		
	КонецЕсли;
	
	Если НЕ Результат = Неопределено Тогда
				
		РасширениеФайла = Результат.РасширениеФайла;
		ДД = Результат.ПолучитьДвоичныеДанные();
		Фото = ПоместитьВоВременноеХранилище(ДД,Новый УникальныйИдентификатор);
		
	КонецЕсли;
	
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УникальныйИдентификаторФормыРодителя = ЭтаФорма.ВладелецФормы.УникальныйИдентификатор;
	
	#Если МобильноеПриложениеКлиент Тогда
		
		Камера = ТипКамерыУстройства.Задняя;
		
		НазваниеФото = ТекущаяДата();
		
	#Иначе
		Отказ = Истина;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура Готово(Команда)
	
	Если НЕ ПустаяСтрока(РасширениеФайла) Тогда
		
		ДанныеФото = Новый Структура;
		ДанныеФото.Вставить("АдресДвоичныеДанные",Фото); 
		ДанныеФото.Вставить("РасширениеФайла",РасширениеФайла);
		ДанныеФото.Вставить("УникальныйИдентификаторФормы",УникальныйИдентификаторФормыРодителя);
		ДанныеФото.Вставить("Идентификатор", ДополнительныеПараметрыРодителя.Идентификатор);
		
		Оповестить("ФотоГотово",ДанныеФото);
		
	КонецЕсли;
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДополнительныеПараметрыРодителя = Новый Структура;
	Если Параметры.Свойство("ДополнительныеПараметрыРодителя") Тогда
		Для каждого КлючЗначение Из Параметры.ДополнительныеПараметрыРодителя Цикл
			ДополнительныеПараметрыРодителя.Вставить(КлючЗначение.Ключ,КлючЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры
